apply plugin: 'groovy'
apply plugin: 'maven'

def compatibilityVersion = 1.5
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion
group = 'murtools'
version = '0.0.1'

def localRepoDir = new File(System.getProperty('user.home'), 'localrepository').absolutePath

repositories {
    mavenCentral()
}

dependencies {
    groovy fileTree(dir: new File(gradle.gradleHomeDir, 'lib'), includes: ['**/groovy-all-*.jar'])
    compile gradleApi()

	testCompile 'org.fusesource.scalate:scalate-core:1.5.3'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Scalate plugin',
                   'Implementation-Version': version,
                   'Built-By': 'ryo-murai',
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0'
}

task sourcesJar(type: Jar) {
    description 'An archive of the source code'
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        ivy {
            url localRepoDir
        }
    }
}

clean << {
	delete 'testproject/lib'
}

task putJar2testproject(type: Copy, dependsOn: jar) {
	from jar.archivePath
	into 'testproject/lib'
}

task testprojectTest(type: GradleBuild, dependsOn: putJar2testproject) {
	buildFile = 'testproject/build.gradle'
	tasks = ['scalatePreCompile', 'test']
}

task testprojectClean(type: GradleBuild, dependsOn: putJar2testproject) {
	buildFile = 'testproject/build.gradle'
	tasks = ['clean']
}

test << {
	testprojectClean.execute()
	testprojectTest.execute()
}
test.dependsOn(putJar2testproject)
